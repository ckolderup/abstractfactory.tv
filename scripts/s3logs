#! /usr/bin/env awk -f
# Write S3 Log Format to Apache Combined Log Format
#
#   http://docs.aws.amazon.com/AmazonS3/latest/dev/LogFormat.html
#   http://httpd.apache.org/docs/1.3/logs.html#combined
#

{
  # NF 20 is the beginning of the User Agent String
  # The last field (NF) is the Version ID
  # This loop concatenates the UA string together
  ua=$20
  for (i=21; i<NF; i++) {
    ua=(ua " " $i);
  }
  print $5,"-",$6,$3,$4,$10,$11,$12,$13,$15,$19,ua;
}
#
# If using the aws:log Rake tasks, pipe the output to
#   tail -n +2
# in order to cut out the first line
#
# Some one liners to pipe this into to get interesting information:
#
#   Print User Agents requesting the feed:
#   awk -F\" '($2 ~ /feed\.xml/) { print $6 }
#
#   Get uniq user agents (could be used with the above as well):
#   awk -F\" '{print $6}' | sort | uniq -c | sort -fr
#
#   Downloaded MP3s (partial downloads) by IP address
#   awk -F\" '($2 ~ /GET\ .+\.mp3/) { print }' | awk '($9 == 206) { print $1,$7 }'
#
#     and from that you can do: sort | uniq -c | sort -r and finally a wc -l
#     will give you number of unique ip addresses that downloaded something
#
#   Get Error HTTP Responses...
#   awk '($9 > 399) { print $9,$7 }'
#
#   For 206 responses, print out IP addresses and the total bytes downloaded
#   awk '($9 == 206){ addresses[$1] += $10; } END { for (x in addresses) { print x, addresses[x]; } }'
#
#   Then sum that total with
#   awk '{ sum += $2 } END { print sum }'
#
